/*
 * Copyright 2015 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
  }

}

plugins {
  id "de.undercouch.download" version "3.1.0"
}

description = 'A swagger-ui mod that supports rfc6570 compliant urls'
group = 'io.springfox.ui'
version = '1.0.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply from: 'gradle/publishing.gradle'

ext {
  swaggerUiVersion = '2.2.6-rfc6570'
  swaggerUiDist = "build/libs/swagger-ui-dist.zip"
  swaggerUiExplodedDir = "swagger-ui-${swaggerUiVersion}/dist/"
  downloadUrl = "https://github.com/dilipkrish/swagger-ui/archive/v${swaggerUiVersion}.zip"
  destinationZip = "$buildDir/zip/${swaggerUiVersion}.zip"
  replacePath = 'window.location.origin + "\\${pageContext.request.contextPath}/api-docs"'
  artifactRepoBase = 'http://oss.jfrog.org/artifactory'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
}

task swaggerUiDownload << {
  if (!file(destinationZip)?.exists()) {
    download {
      src downloadUrl
      dest destinationZip
      onlyIfNewer true
    }
  }
}

task unzip(type: Copy) {
  from zipTree(file(destinationZip))
  include "*/dist/**"
  into file("${buildDir}")
}

task customizeSwaggerUi << {
  copy {
    from("${projectDir}/src/web/js/springfox.js")
    into("${buildDir}/${swaggerUiExplodedDir}")
  }
  copy {
    from("${projectDir}/src/web/fonts")
    into("${buildDir}/${swaggerUiExplodedDir}/fonts")
  }
  delete "${buildDir}/${swaggerUiExplodedDir}/index.html"
  delete "${buildDir}/${swaggerUiExplodedDir}/swagger-ui.js"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

jar {

  from("${projectDir}/src/web/swagger-ui.html") {
    into "META-INF/resources"
  }

  from("${buildDir}/${swaggerUiExplodedDir}") {
    into "META-INF/resources/webjars/springfox-swagger-ui"
    manifest {
      attributes(
          'Built-By': 'Dilip Krishnan',
          'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
          'Build-Date': project.buildDate,
          'Build-Time': project.buildTime,
          'Specification-Title': project.name,
          'Specification-Version': project.version,
          'Implementation-Title': project.name,
          'Implementation-Version': project.version,
          "SwaggerUi-Version": swaggerUiVersion,
      )
    }
  }
}

task cleanup(type: Delete) {
  delete "${buildDir}/${swaggerUiExplodedDir}"
}

clean.dependsOn(cleanup)
compileJava.dependsOn(customizeSwaggerUi)
customizeSwaggerUi.dependsOn(unzip)
unzip.dependsOn(swaggerUiDownload)
